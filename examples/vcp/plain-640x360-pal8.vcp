; -*- mode: vcpasm; tab-width: 4; indent-tabs-mode: nil; -*-
;-----------------------------------------------------------------------------
; This is an example VCP. It is more or less the minimal required program for
; setting up a 640x360 display with a 256 color palette.
;-----------------------------------------------------------------------------

    ; Video registers
    .set    ADDR, 0
    .set    XOFFS, 1
    .set    XINCR, 2
    .set    HSTRT, 3
    .set    HSTOP, 4
    .set    CMODE, 5
    .set    RMODE, 6

    ; CMODE constants
    .set    CM_RGBA8888, 0
    .set    CM_RGBA5551, 1
    .set    CM_PAL8, 2
    .set    CM_PAL4, 3
    .set    CM_PAL2, 4
    .set    CM_PAL1, 5

    ; RMODE constants
    .set    RM_DITHER_NONE, 0
    .set    RM_DITHER_WHITE, 1


    ; Set the program start address
    .org    0x000004

layer1_start:
    jmp     main
    nop
    nop
    nop

layer2_start:
    setpal  0, 1
        .word    0x00000000
    waity   32767
    nop

main:
    ; Set the video mode
    setreg  XINCR, 0x005555   ; 640 pixels/row
    setreg  CMODE, CM_PAL8

    ; Set the palette
    jsr     load_palette_a

    ; Activate video output starting at row 0.
    waity   0
    setreg  HSTOP, 1920

    ; Generate video addresses for all rows.
    .set    row, 0
    .set    row_addr, 0x001000
    .rept   360
        waity   row
        setreg  ADDR, row_addr
        .set    row, row + 3
        .set    row_addr, row_addr + 160   ; Row stride
    .endr

    ; End of program
    waity   32767

load_palette_a:
    ; Load a palette with 256 colors.
    setpal  0, 256
    .word   0xff00ff00
    .lerp   0x01010101, 0xffffffff, 255
    rts

